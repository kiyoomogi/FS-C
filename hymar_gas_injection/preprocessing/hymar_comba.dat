model new
;call 'flac_python.py'
model large-strain off

zone import 'decovalex_mesh.f3grid'
zone face skin

zone cmodel assign columnar-basalt


;global parameters
[global conf = -6.5e6] ; confinement must be negative or zero
[global u0 = 2.5e6]
[global space = 1]
[global dip = 37]
[global dip2 = 57]
[global young = 3.3e9]
;[global young = 1.6e9]
[global poisson = 0.3]

[global ks = 5e9] ;shear joint stiffness
;[global kn = 1.4e9] ; was  1.4 normal joint stiffness
;[global ks = 25e7]
[global kn = 1.24e9]

[global H = 0.06]
[global r = 0.015] 
[global appliedVel = 3e-9] ; loading velocity

;fluid
model configure fluid-flow
zone fluid cmodel assign anisotropic
zone fluid property fluid-density 1000
zone fluid property biot-modulus 10e9 ;12 GPa for solid grain bulk mod (Keller, 2017)
zone fluid property pore-pressure-generation on 
zone fluid property porosity 0.16 
;zone fluid property porosity 1e-6 range group 'BNDBO'
;zone fluid property porosity 1e-6 range group 'BNDTO'; Porosity
zone fluid property mobility-coefficient 5e-18 ; Permeability / viscosity
zone gridpoint initialize pore-pressure [u0]


;initialize and BC
zone initialize stress xx [conf] yy [conf] zz [conf] xy 0 xz 0 yz 0
zone face apply velocity-z 0 range group 'Bottom'
zone face apply velocity-z [-appliedVel] range group 'Top'
zone gridpoint fix velocity-y 0 range position-z 0.06 0.063
zone gridpoint fix velocity-x 0 range position-z 0.06 0.063
zone face apply stress-normal [conf] range group 'East' or 'West'


;retrieve q and strain
[global gps_top = list(gp.list)(gp.isgroup(::gp.list,'Top'))]
[global gps_bot = list(gp.list)(gp.isgroup(::gp.list,'Bottom'))]

[global area = 3.14 * r * r]
fish define q
  local force_top = list.sum(gp.force.unbal(::gps_top)->z)
  global q = ((force_top/area) + conf) * 1e-6
end


[global gp_top1 = gp.near(0,0,0.06)]
[global zlength = H]
fish define strain_zz
  global strain_zz = 100.0*1.0*gp.disp.z(gp_top1)/0.06
end

fish define pp_avg
    local n = list.size(gps_top) + list.size(gps_bot)                   ; count of gridpoints
    local s = list.sum(gp.pp(::gps_top)) + list.sum(gp.pp(::gps_bot))   ; map gp.pp over the list
    pp_avg = ((s / n) - u0) * 1e-6
end

[global gp_side1 = gp.near(0,0.015,0.03)]
fish define strain_yy
  global strain_yy = 100.0*1.0*gp.disp.y(gp_side1)/0.0015
end

fish define V0_m
    V0_m = 0.0
    loop foreach local z zone.list
        V0_m += zone.vol(z)
    end_loop
    global V0_m
end
[V0_m]

fish define epsv_pct
    local Vdef = 0.0
    loop foreach local z zone.list
        Vdef += zone.vol.deformed(z)
    end_loop
    global epsv_pct = 100.0 * (V0_m - Vdef) / V0_m   ; compression negative
end

fish define p_tot
    local sig3 = -conf * 1e-6        ; MPa (σ3 > 0 in compression)
    local sig1 = list.sum(gp.force.unbal(::gps_top)->z) / area            ; since q = σ1 - σ3 (MPa)
    global p_tot = (sig1*1e-6 + 2.0*sig3) / 3.0 - (pp_avg + u0*1e-6)
end


; mean effective stress p' = p - u  (MPa)
fish define p_eff
    local ubar = (u0 * 1e-6) + pp_avg   ; absolute pore pressure (MPa)
    global p_eff = p_tot ;- ubar
end

[global gp_sel = list]

fish define select_initial_band
    local sel = list
    loop foreach local g gp.list(gp.isgroup(::gp.list,"INJEC"))
        local p = gp.pos(g)  ; initial position (reference)
        if p->z >= 0.025 & p->z <= 0.035 then
            sel = list.append(sel, g)

        endif
    endloop
    gp_sel = sel
    ;io.out("Stored ", list.size(gp_sel), " gridpoints in initial z-band [0.024, 0.035].")
    local ztop = list
    loop foreach local gg zone.list(zone.isgroup(::zone.list,"rocks"))
        local pp = zone.pos(gg)  ; initial position (reference)
        if pp->z >= 0.001 & pp->z <= 0.002 then
            ztop = list.append(ztop, gg)

        endif
    endloop
    z_top = ztop
end
[select_initial_band]

fish define radial_strain
    if list.size(gp_sel) = 0 then
        return 0.0
    endif

    local sum = 0.0
    local count = 0

    loop foreach local g gp_sel
        local r0 = 0.015
        if r0 > 0.0 then
            local u  = gp.disp(g)        ; current displacement

            ; radial displacement by projection onto initial radial unit vector
            local ur = math.sqrt(u->x*u->x + u->y*u->y)
            local eps_r = ur / r0

            ; accumulate & store per-point diagnostics
            sum   = sum   + eps_r
            count = count + 1

        endif
    endloop

    if count > 0 then
        return 100.0 * sum / count   ; average radial strain in %
    else
        return 0.0
    endif
end


;mechanical parameters
zone initialize density 2600
zone property cohesion 1e10 ;very high
zone property friction 15
zone property tension 3e8
zone property young [young]
zone property poisson [poisson]

;joint parameters
zone property dip-1 [dip]
zone property dip-direction-1 0
zone property joint-tension-1 1e50
zone property dip-2 [dip2]
zone property dip-direction-2 0
zone property joint-tension-2 1e50

zone property joint-dilation-1 1 
zone property joint-dilation-2 1

table 'fric-1' add (0 9) (0.000157115 10.4304) (0.000348483 11.8604) (0.000586682 13.2902) (0.000891299 14.7197) (0.00129461 16.1489) (0.00185386 17.5779) (0.00268114 19.0065) (0.0040299 20.4349) (0.00662012 21.8629) (0.007 22) (0.0073 22) (0.00743878 20.7228) (0.00768373 19.2899) (0.00814105 17.8584) (0.0092983 16.4285) (0.018 15)
table 'coh-1' add (0 340000) (0.000157115 395165.338713) (0.000348483 450828.089011) (0.000586682 507066.229213) (0.000891299 563960.268182) (0.00129461 621593.314827) (0.00185386 680053.270074) (0.00268114 739432.559035) (0.0040299 799828.580853) (0.00662012 861344.833388) (0.007 867313.206414) (0.0073 867313.206414) (0.00743878 812135.60746) (0.00768373 751328.692809) (0.00814105 691637.985096) (0.0092983 632960.652218) (0.018 575199.970653)


;do not touch 10 MPa!
;table 'fric-2' add (0 7) (0.000200436 8.21564) (0.000444971 9.43106) (0.000749959 10.6463) (0.00114098 11.8612) (0.00166041 13.076) (0.00238394 14.2906) (0.00346127 15.5049) (0.00523587 16.719) (0.00870861 17.9329) (0.009 18) (0.0093 18) (0.00957422 16.8601) (0.0100233 15.6441) (0.0108147 14.4288) (0.0125803 13.2141) (0.02 12)
;table 'coh-2' add (0 1800000999) (0.000200436 2116597.68845) (0.000444971 2435083.854283) (0.000749959 2755765.048585) (0.00114098 3078955.30238) (0.00166041 3404979.581262) (0.00238394 3734172.880051) (0.00346127 4066881.19772) (0.00523587 4403465.321974) (0.00870861 4744301.126524) (0.009 4763265.421308) (0.0093 4763265.421308) (0.00957422 4442858.129482) (0.0100233 4105272.622914) (0.0108147 3771853.773608) (0.0125803 3442228.47534) (0.02 3116041.692804)

; 4 MPa
table 'fric-2' add (0 8) (0.000277778 8.78606) (0.000597893 9.57207) (0.000970822 10.358) (0.00141081 11.1439) (0.00193774 11.9298) (0.0025802 12.7156) (0.00338085 13.5013) (0.004 14) (0.0041 14) (0.00415897 13.7206) (0.00436682 12.9328) (0.00466857 12.1453) (0.0051464 11.3584) (0.00601796 10.5718) (0.00811082 9.78569) (0.02 9)
table 'coh-2' add (0 1800000999) (0.000277778 1979541.058328) (0.000597893 2159832.346459) (0.000970822 2340947.54403) (0.00141081 2522961.04237) (0.00193774 2705948.495657) (0.0025802 2889987.095008) (0.00338085 3075155.972167) (0.004 3193309.660271) (0.0041 3193309.660271) (0.00415897 3127051.721123) (0.00436682 2941054.090863) (0.00466857 2756329.147107) (0.0051464 2572792.583526) (0.00601796 2390366.834051) (0.00811082 2208972.998576) (0.02 2028534.932128)



table 'fric-b' add (0 30) (1 30) 
table 'coh-b' add (0 1e9) (1 1e9) ;(1e-2 0.5e6) (9e-2 0.2e6)


zone property table-joint-friction-1 'fric-1' range group 'INJEC'
zone property table-joint-cohesion-1 'coh-1' range group 'INJEC'
zone property table-joint-friction-2 'fric-2' range group 'INJEC'
zone property table-joint-cohesion-2 'coh-2' range group 'INJEC'
zone property table-joint-friction-1 'fric-b' range group 'BNDTO'
zone property table-joint-cohesion-1 'coh-b' range group 'BNDTO'
zone property table-joint-friction-2 'fric-b' range group 'BNDTO'
zone property table-joint-cohesion-2 'coh-b' range group 'BNDTO'

zone property space-1 [space]
zone property stiffness-normal-1 [kn]
zone property stiffness-shear-1 [ks]

zone property space-2 [space]
zone property stiffness-normal-2 1e11
zone property stiffness-shear-2 1e11

zone property flag-matrix-plastic on

history interval 30
fish history q
fish history strain_zz
fish history pp_avg
fish history strain_yy
fish history V0_m
fish history epsv_pct
fish history p_tot
fish history radial_strain


;model mechanical timestep automatic off
model cycle 75000

history export 1 vs 2 table '1'
table '1' export 'q_strain37' truncate
history export 3 vs 2 table '2'
table '2' export 'pp_strain37' truncate
history export 6 vs 2 table '4'
table '4' export 'evol_ax37' truncate
history export 1 vs 7 table '5'
table '5' export 'q_p37' truncate
history export 1 vs 8 table '6'
table '6' export 'q_radstr37' truncate



model save 'confinedCompressionMultiZones'
